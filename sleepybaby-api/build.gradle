buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${versions.springBoot}")
    classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}")
    classpath("org.jetbrains.kotlin:kotlin-allopen:${versions.kotlin}")
    classpath('com.netflix.nebula:gradle-ospackage-plugin:4.4.0')
    classpath('org.junit.platform:junit-platform-gradle-plugin:1.0.0-M4')
  }
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'org.springframework.boot'
apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'nebula.ospackage'
apply plugin: 'application'

repositories {
  mavenCentral()
  jcenter()

  maven { url "http://dl.bintray.com/jetbrains/spek" }
}

dependencies {
  compile('org.springframework.boot:spring-boot-starter-security')
  compile('org.springframework.boot:spring-boot-starter-web')
  compile("org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}")
  compile("org.jetbrains.kotlin:kotlin-reflect:${versions.kotlin}")

  compile('org.apache.commons:commons-csv:1.4')
  compile('io.jsonwebtoken:jjwt:0.7.0')
  compile('com.fasterxml.jackson.module:jackson-module-kotlin:2.8.7')

  testCompile('org.springframework.boot:spring-boot-starter-test')
  testCompile('org.springframework.restdocs:spring-restdocs-mockmvc')

  testCompile("org.jetbrains.spek:spek-api:${versions.spek}")
  testCompile("org.jetbrains.spek:spek-subject-extension:${versions.spek}")
  testCompile("com.natpryce:hamkrest:${versions.hamkrest}")

  testRuntime("org.jetbrains.spek:spek-junit-platform-engine:${versions.spek}")
}

processResources.dependsOn(':sleepybaby-web-ng2:build')

tasks.build.finalizedBy tasks.buildDeb
tasks.buildDeb.dependsOn { installDist }

mainClassName = "org.jordens.sleepybaby.MainKt"
applicationName = "sleepybaby"

bootRepackage.enabled = false

springBoot {
  mainClass = "org.jordens.sleepybaby.MainKt"
}

jar {
  baseName = 'sleepybaby'
  version = '0.0.1-SNAPSHOT'

  from('../sleepybaby-web-ng2/dist') {
    into 'public' // public is a default supported Spring Boot resources directory.
  }
}

ospackage {
  from("build/install/sleepybaby") {
    into "/apps/sleepybaby"
  }

  from("root") {
    into "/"
  }
}

junitPlatform {
  filters {
    engines {
      include 'spek'
    }
  }
}

