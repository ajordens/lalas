buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
    classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
    classpath('com.netflix.nebula:gradle-ospackage-plugin:4.4.0')
  }
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'org.springframework.boot'
apply plugin: 'nebula.ospackage'
apply plugin: 'application'

repositories {
  mavenCentral()
  jcenter()
}

dependencies {
  compile('org.springframework.boot:spring-boot-starter-data-rest')
  compile('org.springframework.boot:spring-boot-starter-hateoas')
  compile('org.springframework.boot:spring-boot-starter-security')
  compile('org.springframework.boot:spring-boot-starter-web')
  compile("org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}")
  compile("org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}")

  compile('org.apache.commons:commons-csv:1.4')
  compile('io.jsonwebtoken:jjwt:0.7.0')
  compile('com.fasterxml.jackson.module:jackson-module-kotlin:2.8.7')

  testCompile('org.springframework.boot:spring-boot-starter-test')
  testCompile('org.springframework.restdocs:spring-restdocs-mockmvc')
}

processResources.dependsOn(':sleepybaby-web-ng2:build')

tasks.build.finalizedBy tasks.buildDeb
tasks.buildDeb.dependsOn { installDist }

mainClassName = "org.jordens.sleepybaby.MainKt"
applicationName = "sleepybaby"

jar {
  baseName = 'sleepybaby'
  version = '0.0.1-SNAPSHOT'

  from('../sleepybaby-web-ng2/dist') {
    into 'public' // public is a default supported Spring Boot resources directory.
  }
}

ospackage {
  from("build/install/sleepybaby") {
    into "/apps/sleepybaby"
  }

  from("root"){
    into "/"
  }
}

bootRepackage.enabled = false

springBoot {
  mainClass = "org.jordens.sleepybaby.MainKt"
}
